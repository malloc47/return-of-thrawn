# Jedi Knight Cog Script
#
# 09_FuelExplode.cog
#
# This script makes a section of fuel pipes "explode" when 
# a weapon bolt hits the fuel.
#
# [YB/IS/RD]
#
# (C) 1997 LucasArts Entertainment Co. All Rights Reserved		
# ========================================================================================

symbols

surface	surf0		mask=0x8,linkid=0
surface	surf1		mask=0x8,linkid=1
surface	surf2		mask=0x8,linkid=2
surface	surf3		mask=0x8,linkid=3
surface	surf4		mask=0x8,linkid=4
surface	surf5		mask=0x8,linkid=5
surface	surf6		mask=0x8,linkid=6
surface	surf7		mask=0x8,linkid=7
surface	surf8		mask=0x8,linkid=8

int		num_surfaces=0	local

template	explosion_tpl=+tie_exp	local

int	i=0			local
int	j=0			local
int	explosion		local
int	locked=0		local
int	tempVec		local

flex	multiboom=1.0		// multiplier for extending boomtime
flex	scaleX=1.0
flex	scaleY=1.0
flex	scaleZ=0.7

message	startup
message     crossed
										  
end

# ========================================================================================

code
startup:
	for (i=0; i<9; i=i+1)
	{
		if (surf0[i] == -1) return;
		num_surfaces = num_surfaces + 1;
	}
      return;

crossed:
	if (GetFaceGeoMode(GetSenderRef()) == 0) return;	// Face not rendered, fuel at different level, exit
	
	// This makes an explosion at the "impact" point.
	explosion = CreateThing(explosion_tpl, GetSourceRef());
	DestroyThing(GetSourceRef());

	// Ignore any new cross while in the loops,
	// except the impact point explosion above.
        
	if (locked) Return;
	locked = 1;

	for(i=GetSenderId(); i>=0; i=i-1)
	{      
		for(j=0; j<4+(multiboom * 12 * Rand()); j=j+1)
		{
		sleep(.2);

		tempVec = VectorSet(scaleX*(Rand()-0.5), scaleY*(Rand()-0.5), scaleZ*Rand());
		tempVec = VectorAdd(SurfaceCenter(surf0[i]), tempVec);

		explosion = CreateThingAtPos(explosion_tpl, GetSurfaceSector(surf0[i]), tempVec,
						     '0.0 0.0 0.0');
            }
	}

	for(i=GetSenderId()+1; i < num_surfaces; i=i+1)
	{
		for(j=0; j<4+(multiboom * 12 * Rand()); j=j+1)
		{
		sleep(.2);
		tempVec = VectorSet(scaleX*(Rand()-0.5), scaleY*(Rand()-0.5), scaleZ*Rand());
		tempVec = VectorAdd(SurfaceCenter(surf0[i]), tempVec);

		explosion = CreateThingAtPos(explosion_tpl, GetSurfaceSector(surf0[i]), tempVec,
						     '0.0 0.0 0.0');
		}
	}
        
	locked = 0;
	return;

end

